// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) {
            let maxLength = 1;
        }
        let str = Array.new(maxLength);
        let length = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do str.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {     
        let str[length] = c; 
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length = 0) {
            let str[0] = null;
            let length = 0;

        }
        else {
            let str[length-1] = null;
            let length = length - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        return String.string2Int(this);
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var String newStr;
        var int i, newStrLength, absVal;

        let i = 0;
        let absVal = Math.abs(val);
        let newStr = String.int2String(absVal);
        let newStrLength = newStr.length();

        do str.dispose();
        let length = 0;

        if (val < 0) {
            let newStrLength = newStr.length() + 1;
            let str = Array.new(newStrLength);
            do appendChar(45);
        }
        else {
            let newStrLength = newStr.length();
            let str = Array.new(newStrLength);
        }

        while (i < newStrLength) {
            do appendChar(newStr.charAt(i));
            let i = i + 1;
        }
        do newStr.dispose();
        return;
    }

    function String int2String(int val) {
        var int lastDigit;
        var String newStr;
        var char c;

        let lastDigit = val - (10 * (val/10));
        let c = (48 + lastDigit);

        if (val < 10) {
            let newStr = String.new(6);
            do newStr.appendChar(c);
            return newStr;
        }
        else {
            let newStr = String.int2String(val / 10);
            let newStr = newStr.appendChar(c);
            return newStr;
        }
    }

    function int string2Int(String str) {
        var int val, i, d, sign;
        let val = 0;

        if (str.charAt(0) = 45) {
            let sign = -1;
            let i = 1;
        }
        else {
            let sign = 1;
            let i = 0;
        }

        while (i < str.length()) {
            let d = str.charAt(i) - 48;
            let val = (val * 10) + d;
            let i = i + 1;
        }

        return (val * sign);
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
